MacroName MontageTiltSerieswithBeamCentering

##Realign to center image up to 3 times when the shift is more than 1um, end with nonzero image shift
##Center reference image taken at view mode and saved in buffer T
LmtRATimes = 3
LmtISShift = 1
ISDelay = 0.5
Beamcentering = 0

Require arrays
ReportNavItem

## Get data directory, image prefix, tile coordinate file, number of tilts in series 
## and montage tiles per tilt from commandline
SetDirectory X:\DoseFractions\Brown\20240930_Setupscripttests
OFPrefix = MontageNeurons2_$navLabel

# TODO store an imageshifts file for each nav item, these will be set up
# by another serial-EM script
ParFilename = Montage_imageshifts_$navLabel.txt


## Initialize arrays for storing stage positions
NewArray PlusStagePos -1 2
NewArray MinusStagePos -1 2

## Opens text file $ParFilename as read-only (r) and with ID MTSPar
OpenTextFile MTSPar r 0 $ParFilename
## Read line from image shift text file (id MTSPar) to array TSNumber (number of tilt series)
NewArray TSNumber -1 1
ReadLineToArray MTSPar TSNumber 
echo $TSNumber

## Initialize Angle, Montage image shift (MontIS_D) and last Montage Image shifts (LastMontIS_D)
## arrays
NewArray Angle -1 1
NewArray MontIS_D -1 3
NewArray LastMontIS_D -1 3 
echo $TSNumber

# Get matrix to convert from camera values for image shift (which is what is inputted)
# to
GoToLowDoseArea R
CameraToSpecimenMatrix 0

# 2 * is to account for the fact that serial-EM takes the "un-binned" pixel size of the K3 to be in super-res mode
XperX = 2 * $ReportedValue1
XperY = 2 * $ReportedValue2
YperX = 2 * $ReportedValue3
YperY = 2 * $ReportedValue4

## Loop over tilts
Loop $TSNumber

   ## Reset counter for montage tiles and image shift
   Count = 0
   SetImageShift 0 0

   ## Read line from image shift text file (id MTSPar) to array Angle
   ReadLineToArray MTSPar Angle

   ## Write tilt angle to log window
   Echo Tilt to $Angle
   
   ## 0 degree image
   If $Angle == 0
      TiltTo $Angle
      ResetImageShift 
      RealignToNavItem  1 1 $LmtISShift $LmtRATimes  3

      View
      ## Copy View image to buffers R and S, R will be overwritten at each
      ## positive tilt and S will be overwritten at each negative tilt
      Copy A R
      Copy A S

      ## Get and store stage positions, positive and negative (tilt) stage
      ## positions will be stored seperately
      ReportStageXYZ
      PlusStagePos[1] = $ReportedValue1
      PlusStagePos[2] = $ReportedValue2
      MinusStagePos = $PlusStagePos

      
      ## 1 means beam centering using centroid - beam should be fully in image
      If $Beamcentering == 1
         Trial 
         CenterBeamFromImage
       Endif

      ## Autofocus, take Trial image and center beam if necessary
      AutoFocus 

      #Read number of tiles to variable TileNumber
      ReadLineToArray MTSPar TileNumber
     

      ## Get image shift from file and store in array
      ReadLineToArray MTSPar LastMontIS_D
      echo  $Count  $LastMontIS_D

      ## Convert image shift from camera pixel units to specimen units
      ## by multiplying by appropriate matrix
      Xout = $LastMontIS_D[1] * $XperX + $LastMontIS_D[2] * $XperY
      Yout = $LastMontIS_D[1] * $YperX + $LastMontIS_D[2] * $YperY 
      LastMontIS_D[1] = $Xout
      LastMontIS_D[2] = $Yout

      ## Apply image shift from file
      ImageShiftByMicrons $Xout $Yout

      ## Open output file
      output_file = $OFPrefix_$Angle.mrc
      OpenNewFile $output_file 

      ## Delay after image shift and then record image save, and
      ## close file
      Delay $ISDelay
      Record
      Save
      
      # beam centering using centroid 1 means beam should be fully in image
      If $Beamcentering == 1
         CenterBeamFromImage 1
      Endif

      # Loop over times
      Echo $TileNumber tiles for this tilt
      Loop $TileNumber - 1 indx 
         ## Update count (for saving image shift tiles)
         Count = $Count + 1

         ## Read image shift from file
         ReadLineToArray MTSPar MontIS_D
         echo  $indx $Count  $MontIS_D

         ## Convert image shift from camera pixel units to specimen units
         ## by multiplying by appropriate matrix
         Xout = $MontIS_D[1] * $XperX + $MontIS_D[2] * $XperY
         Yout = $MontIS_D[1] * $YperX + $MontIS_D[2] * $YperY 

         ## Difference in image shift from last image shift vector
         IS_X = $Xout - $LastMontIS_D[1]
         IS_Y = $Yout - $LastMontIS_D[2]
         ImageShiftByMicrons $IS_X $IS_Y
         ReportSpecimenShift

         # Open output file
         # output_file = $OFPrefix_$Angle_$Count.mrc
         # OpenNewFile $output_file

         ## Delay after image shift and then record image, save image, and
         ## close file
         Delay $ISDelay
         Record
         Save 

         # Center beam
         If $Beamcentering == 1
            CenterBeamFromImage 1
         Endif

         # Store current image shift as last image shift for next loop
         # iteration
         LastMontIS_D[1] = $Xout
         LastMontIS_D[2] = $Yout

      EndLoop

      # Close file
      CloseFile

   # Positive tilts
   ElseIf $Angle > 0
      TiltTo $Angle

      # Move stage to previously aligned x,y position
      MoveStageTo $PlusStagePos[1] $PlusStagePos[2]

      ## Loop alignment procedure at view preset up to
      ## $LmtRATimes (3 by default)
      Loop $LmtRATimes
         ## Take view image
         View

         ## Align to image stored in buffer R (view image taken at previous tilt)
         AlignTo R
         ReportSpecimenShift 
         
         ## Magnitude of shift by Pythagoras theorem
         IS = $reportedValue1 * $reportedValue1 + $reportedValue2 * $reportedValue2

         ## If measured shift greater than threshhold reset image shift and stage shift
         ## to position, then align again. Otherwise break loop
         If $IS > $LmtISShift
            ResetImageShift
         Else
            Break 
         Endif 
      EndLoop
      ## Save aligned view image in buffer R
      View
      Copy A R
      ReportStageXYZ
      PlusStagePos[1] = $ReportedValue1
      PlusStagePos[2] = $ReportedValue2
      
      ## Autofocus and center beam

         Trial 
         CenterBeamFromImage
      AutoFocus
      


      # Read number of tiles from file
      ReadLineToArray MTSPar TileNumber
      Echo $TileNumber tiles for this tilt

      ## Get image shift from file for first tile and apply it
      ReadLineToArray MTSPar LastMontIS_D

      ## Convert image shift from camera pixel units to specimen units
      ## by multiplying by appropriate matrix
      Xout = $LastMontIS_D[1] * $XperX + $LastMontIS_D[2] * $XperY
      Yout = $LastMontIS_D[1] * $YperX + $LastMontIS_D[2] * $YperY 
      LastMontIS_D[1] = $Xout
      LastMontIS_D[2] = $Yout

      ## Apply image shift from file
      ImageShiftByMicrons $Xout $Yout

      ## Change focus if required
      if $LastMontIS_D[3] != 0
         ChangeFocus $LastMontIS_D[3]
         echo chang focus by $LastMontIS_D[3]
      endif

      ## Make output file
      output_file = $OFPrefix_$Angle.mrc
      OpenNewFile $output_file 

      ## Delay after image shift and then record image, save image, and
      ## close file
      Delay $ISDelay
      Record
      Save
      Echo $output_file saved

      ## Center beam from record image (using centroid - beam should be fully in image)
      If $Beamcentering == 1
         CenterBeamFromImage 1
      Endif


      ## Loop over montage tiles
      Loop $TileNumber - 1 
         ## update counter for montage tiles
         Count = $Count + 1

         ## Read image shift from file
         ReadLineToArray MTSPar MontIS_D

         ## Convert image shift from camear pixel units to specimen units
         ## by multiplying by appropriate matrix
         Xout = $MontIS_D[1] * $XperX + $MontIS_D[2] * $XperY
         Yout = $MontIS_D[1] * $YperX + $MontIS_D[2] * $YperY 

         ## Difference in image shift from last image shift vector
         IS_X = $Xout - $LastMontIS_D[1]
         IS_Y = $Yout - $LastMontIS_D[2]

         ## Defocus change
         DelDef = $MontIS_D[3] - $LastMontIS_D[3]

         ## Apply image shift
         ImageShiftByMicrons $IS_X $IS_Y

         ## Apply defocus offset if necessary
         if $DelDef != 0
            ChangeFocus $DelDef
            echo change focus by $DelDef
         Endif

         ## Get current image shift (necessary?)
         ReportSpecimenShift

         ## Open outputfile for this tile
         # output_file = $OFPrefix_$Angle_$Count.mrc
         # OpenNewFile $output_file 

         ## Delay after image shift and then record image, save image, and
         ## close file
         Delay $ISDelay
         Record
         Save
          
         Echo $output_file saved
         If $Beamcentering == 1
            CenterBeamFromImage 1
         Endif

         # Store current image shift as last image shift for next loop
         # iteration
         LastMontIS_D[1] = $Xout
         LastMontIS_D[2] = $Yout
         LastMontIS_D[3] = $MontIS_D[3]
      EndLoop
      CloseFile
   ## Negative tilts
   Else

      ## Tilt to angle (first go -1 below target)
      TiltTo ($Angle - 1)
      TiltTo $Angle

      ## Move stage to last aligned x and y 
      MoveStageTo $MinusStagePos[1] $MinusStagePos[2]

      ## Apply aligned to last saved (negative tilt) view image
      Loop $LmtRATimes
         View
         AlignTo S
         ReportSpecimenShift 
         IS = $reportedValue1 * $reportedValue1 + $reportedValue2 * $reportedValue2
         If $IS > $LmtISShift
            ResetImageShift
         Else
            Break 
         Endif 
      EndLoop

      ## Update view image for negative tilts and save stage position
      View
      Copy A S
      ReportStageXYZ
      MinusStagePos[1] = $ReportedValue1
      MinusStagePos[2] = $ReportedValue2
      
      ## Autofocus and center beam
      # Center beam
      If $Beamcentering == 1
         Trial 
         CenterBeamFromImage
      Endif

      AutoFocus

      # Read number of tiles from file
      ReadLineToArray MTSPar TileNumber
      Echo $TileNumber tiles for this tilt      

      ## Get image shift from file for first tile and apply it
      ReadLineToArray MTSPar LastMontIS_D
      
      ## Convert image shift from camera pixel units to specimen units
      ## by multiplying by appropriate matrix
      Xout = $LastMontIS_D[1] * $XperX + $LastMontIS_D[2] * $XperY
      Yout = $LastMontIS_D[1] * $YperX + $LastMontIS_D[2] * $YperY 
      LastMontIS_D[1] = $Xout
      LastMontIS_D[2] = $Yout

      ## Apply image shift from file
      ImageShiftByMicrons $Xout $Yout

      ## Change focus if required
      if $LastMontIS_D[3] != 0
         ChangeFocus $LastMontIS_D[3]
         echo change focus by $LastMontIS_D[3]
      Endif

      ## Make output file
      output_file = $OFPrefix_$Angle.mrc
      OpenNewFile $output_file 

      ## Delay after image shift and then record image, save image, and
      ## close file
      Delay $ISDelay
      Record
      Save
      Echo $output_file saved

      ## Center beam from record image (using centroid - beam should be fully in image)
      If $Beamcentering == 1
         CenterBeamFromImage 1
      Endif

      ## Loop over montage tiles
      Loop $TileNumber - 1
         ## update counter for montage tiles
         Count = $Count + 1

         ## Read image shift from file
         ReadLineToArray MTSPar MontIS_D

         ## Convert image shift from camera pixel units to specimen units
         ## by multiplying by appropriate matrix
         Xout = $MontIS_D[1] * $XperX + $MontIS_D[2] * $XperY
         Yout = $MontIS_D[1] * $YperX + $MontIS_D[2] * $YperY 
         MontIS_D[1] = $Xout
         MontIS_D[2] = $Yout

         ## Vector difference between this image shift and the last
         IS_X = $MontIS_D[1] - $LastMontIS_D[1]
         IS_Y = $MontIS_D[2] - $LastMontIS_D[2]

         ## Defocus change
         DelDef = $MontIS_D[3] - $LastMontIS_D[3]
         if $DelDef != 0
            echo change focus by $DelDef
            ChangeFocus $DelDef
         Endif  

         ## Apply image shift
         ImageShiftByMicrons $IS_X $IS_Y

         ## Get current image shift (necessary?)
         ReportSpecimenShift
         # output_file = $OFPrefix_$Angle_$Count.mrc
         # OpenNewFile $output_file 

         ## Delay after image shift and then record image, save image, and
         ## close file
         Delay $ISDelay
         Record
         Save
         
         Echo $output_file saved
         If $Beamcentering == 1
            CenterBeamFromImage 1
         Endif

         ## Store current image shift for next montage tiles
         LastMontIS_D = $MontIS_D
      EndLoop
      CloseFile 
   Endif 
EndLoop 

TiltTo 0
SetImageShift 0 0
